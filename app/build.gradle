plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-parcelize"
    id "androidx.navigation.safeargs.kotlin"
    id "kotlin-kapt" // Required for annotation processing.
    id "dagger.hilt.android.plugin"
}

ext {
    nav_version = "2.4.2"
    lifecycle_version = "2.4.1"
    retrofit_version = "2.9.0"
    room_version = "2.4.2"
    glide_version = "4.13.1"
    hilt_version = "2.40"
    paging_version = "3.1.1"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.mendelin.tmdb_hilt"
        minSdk 23
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        /* TMDB API key value */
        buildConfigField "String", "TMDB_API_KEY", "\"d85cb2ad11084018f40ede301fa1bf39\""

        /* REST API endpoint names */
        buildConfigField "String", "ENDPOINT_MOVIE_GENRE_LIST", "\"genre/movie/list\""
        buildConfigField "String", "ENDPOINT_TV_GENRE_LIST", "\"genre/tv/list\""

        buildConfigField "String", "ENDPOINT_MOVIE_CREDITS", "\"movie/{movie_id}/credits\""
        buildConfigField "String", "ENDPOINT_MOVIE_NOW_PLAYING", "\"movie/now_playing\""
        buildConfigField "String", "ENDPOINT_MOVIE_POPULAR", "\"movie/popular\""
        buildConfigField "String", "ENDPOINT_MOVIE_TOP_RATED", "\"movie/top_rated\""
        buildConfigField "String", "ENDPOINT_MOVIE_UPCOMING", "\"movie/upcoming\""
        buildConfigField "String", "ENDPOINT_MOVIE_DETAILS", "\"movie/{movie_id}\""

        buildConfigField "String", "ENDPOINT_PERSON_DETAILS", "\"person/{person_id}\""
        buildConfigField "String", "ENDPOINT_PERSON_MOVIE_CREDITS", "\"person/{person_id}/movie_credits\""
        buildConfigField "String", "ENDPOINT_PERSON_TV_CREDITS", "\"person/{person_id}/tv_credits\""

        buildConfigField "String", "ENDPOINT_TV_SEASON_DETAILS", "\"tv/{tv_id}/season/{season_number}\""

        buildConfigField "String", "ENDPOINT_TV_EPISODE_DETAILS", "\"tv/{tv_id}/season/{season_number}/episode/{episode_number}\""

        buildConfigField "String", "ENDPOINT_TV_CREDITS", "\"tv/{tv_id}/credits\""
        buildConfigField "String", "ENDPOINT_TV_ON_THE_AIR", "\"tv/on_the_air\""
        buildConfigField "String", "ENDPOINT_TV_POPULAR", "\"tv/popular\""
        buildConfigField "String", "ENDPOINT_TV_TOP_RATED", "\"tv/top_rated\""
        buildConfigField "String", "ENDPOINT_TV_DETAILS", "\"tv/{tv_id}\""

        /* REST API parameter names */
        buildConfigField "String", "QUERY_API_KEY", "\"api_key\""
        buildConfigField "String", "QUERY_LANGUAGE", "\"language\""
        buildConfigField "String", "QUERY_PAGE", "\"page\""

        buildConfigField "String", "PATH_CREDIT_ID", "\"credit_id\""
        buildConfigField "String", "PATH_MOVIE_ID", "\"movie_id\""
        buildConfigField "String", "PATH_TV_ID", "\"tv_id\""
        buildConfigField "String", "PATH_PERSON_ID", "\"person_id\""
        buildConfigField "String", "PATH_SEASON_NUMBER", "\"season_number\""
        buildConfigField "String", "PATH_EPISODE_NUMBER", "\"episode_number\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            /* Keep local server for DEBUG build type */
            buildConfigField "String", "BASE_URL", "\"https://api.themoviedb.org/3/\""
            buildConfigField("boolean", "RELEASE", "false")
        }

        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

            /* Keep production server for RELEASE build type */
            buildConfigField "String", "BASE_URL", "\"https://api.themoviedb.org/3/\""
            buildConfigField("boolean", "RELEASE", "true")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }

    kapt {
        useBuildCache = true
    }

    packagingOptions {
        resources {
            excludes += ["META-INF/proguard/androidx-annotations.pro"]
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    /* Testing */
    implementation fileTree(dir: "libs", include: ["*.aar", "*.jar"], exclude: [])
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    /* Kotlin */
    implementation "androidx.core:core-ktx:1.7.0"

    /* Android libraries */
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:1.5.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    /* Navigation components */
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    /* Lifecycle, LiveData, ViewModel */
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"

    /* Gson */
    implementation "com.google.code.gson:gson:2.9.0"

    /* Retrofit networking library */
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    /* Room database */
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    /* Image handling */
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:annotations:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    /* Timber logging */
    implementation "com.jakewharton.timber:timber:5.0.1"

    /* Hilt - dependency injection */
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    /* Paging 3 */
    implementation "androidx.paging:paging-runtime:$paging_version"

    /* Datastore */
    implementation "androidx.datastore:datastore-preferences:1.0.0"
}